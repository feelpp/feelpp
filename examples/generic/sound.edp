real kc2=1;
int size=20; //changer  .... 30
func g=4*y*(1-y);

border a0(t=0,1) { x= 5; y= 1+2*t ;}
border a1(t=0,1) { x=5-2*t; y= 3 ;}
border a2(t=0,1) { x= 3-2*t; y=3-2*t ;}
border a3(t=0,1) { x= 1-t; y= 1 ;}
border a4(t=0,1) { x= 0; y= 1-t ;}
border a5(t=0,1) { x= t; y= 0 ;}
border a6(t=0,1) { x= 1+4*t; y= t ;}
mesh Th=buildmesh( a0(size) + a1(size) + a2(size)
        + a3(size) + a4(size) + a5(size) + a6(size));

/*
border a0(t=0,1) { x= t; y= 0 ;}
border a1(t=0,1) { x= 1; y= t ;}
border a2(t=0,1) { x= 1-t; y= 1 ;}
border a4(t=0,1) { x= 0; y= 1-t ;}
mesh Th=buildmesh( a0(size) + a1(size) + a2(size) + a4(size));


border a0(t=0,pi) { x= cos(t); y= sin(t) ;}
border a4(t=pi,2*pi) { x= cos(t); y= sin(t) ;}
mesh Th=buildmesh( a0(size) + a4(size));
*/

fespace Vh(Th,P1);


Vh u,v;
solve sound(u,v)=int2d(Th)(u*v * kc2 - dx(u)*dx(v) - dy(u)*dy(v))
                 - int1d(Th,a4)(g*v);
plot(u, wait=1, value=1);

real sigma = 20;//20;                                      //    value of the shift
                                 //   OP = A - sigma B ; // the shifted matrix
varf op(u1,u2)= int2d(Th)( dx(u1)*dx(u2) + dy(u1)*dy(u2) - sigma* u1*u2 );
varf b([u1],[u2]) = int2d(Th)( u1*u2 ) ;           //    no Boundary condition
matrix OP= op(Vh,Vh,solver=Crout,factorize=1);
//matrix OP= op(Vh,Vh,solver=CG,eps=1e-20);
matrix B= b(Vh,Vh,solver=CG,eps=1e-20);
int nev=2; // changer ... 50                    //   number of requested eigenvalues near sigma
real[int] ev(nev);                            //   to store the nev eigenvalue
Vh[int] eV(nev);                             //   to store the nev eigenvector
int k=EigenValue(OP,B,sym=true,sigma=sigma,value=ev,vector=eV,
                   tol=1e-10,maxit=0,ncv=0);
for (int i=0; i<nev; ++i){
	cout<<ev(i)<<endl;
	v=eV[i];
	plot(v, wait=1, value=1);
}
